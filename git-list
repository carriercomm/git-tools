#!/usr/bin/perl
use 5.10.0;
use strict;
use warnings;

# ----------------------------------------------------------------------

use Getopt::Long;
my ( $help, @heads, $local, $remotes, $tags, $leafs_only );
GetOptions(
    "help|h|?"      => \$help,          # this help text
    "branch|b=s"    => \@heads,         # branch name to operate on
    "local|l"       => \$local,         # check local branches also
    "remotes|r"     => \$remotes,       # check remotes also
    "tags|t"        => \$tags,          # check tags also
    "leafs_only|lo" => \$leafs_only,    # search only leaf refs; see below
) or die "option error; please run with '-h' for help\n";

usage() if $help;                       # exits;

# globals

# ----------------------------------------------------------------------

# forward declarations for some subs; see end of file for code
sub rc;
sub text;
sub lines;
sub try;

# ----------------------------------------------------------------------

# combine search pattern
my $patt = join "|", @ARGV;

# get current branch
my $current = 'HEAD';
try "git symbolic-ref HEAD" and ( $current = (lines)[0] ) =~ s(refs/heads/)();

@heads = split( /,/, join( ',', @heads ) );    # see 'man Getopt::Long' :)
push @heads, '--branches' if $local;
push @heads, '--remotes'  if $remotes;
push @heads, '--tags'     if $tags;
@heads = ($current) unless @heads;

# this also expands things like --branches, using rev-parse
@heads = leaf_only(@heads);

my %seen;
# after all that buildup, this is somewhat of an anti-climax!
for my $h (@heads) {
    try("git ls-tree -r --name-status $h") or die "ls-tree $h failed";
    map { print "$h\t"; say } grep( /$patt/, lines() );
}

# DONE...

# ----------------------------------------------------------------------
# subroutines
# ----------------------------------------------------------------------

sub replace_full_refs {
    my @refs;
    local @_ = @_;    # defeat the implicit aliasing in the 'for' loop below

    for my $r (@_) {
        $r =~ s(^refs/(heads|tags|remotes)/)();    # was a proper ref
        push @refs, $r;
    }

    return @refs;
}

# kill the internal nodes (there should be a git supplied way to do this, no?)
sub leaf_only {
    # also uses $leafs_only
    my @rev_parse_args = @_;
    my ( %nonleaf, %leaf );

    my $cmd = "git rev-parse --symbolic " . join( " ", @rev_parse_args );
    chomp( my @refs = `$cmd` );
    @refs = replace_full_refs(@refs);
    return @refs unless $leafs_only;

    # '--simplify-by-decoration' is key -- with that flag, git prints the next
    # decoration line's SHA (which may in reality be many commits ago) as the
    # parent instead of the real parent commit.

    $cmd = "git log --format=%h:%p:%d --simplify-by-decoration " . join( " ", @rev_parse_args );
    for (`$cmd`) {
        chomp;
        my ( $h, $p, $d ) = split /:/;

        # mark parents as non-leaf
        for my $p1 ( split ' ', $p ) {
            $nonleaf{$p1}++;
        }

        # if we're non-leaf, we're done; bail out
        next if $nonleaf{$h};

        # find our decorations
        if ($d) {
            $d =~ s/ \(//;
            $d =~ s/\)//;
            my @d = split /, /, $d;

            # check our list of refs in order
            for my $r (@refs) {
                if ( $r ~~ @d ) {
                    $leaf{$r} = 1;    # and mark the first
                    last;             # don't mark any of the others
                }
            }
        }
    }

    # return leaf refs, preserving the original order of @refs
    return grep { $leaf{$_} } @refs;
}

# ----------------------------------------------------------------------

sub usage {
    # I don't like POD.  This is not up for discussion.

    say "
git-ls -- list files in repo

Usage: git ls [options] [list of pathname regexes]
";

    @ARGV = ($0);
    while (<>) {
        next unless /^\s*GetOptions/ .. /^\s*\)/;
        next if /^\s*GetOptions/ or /^\s*\)/;

        my $op = '';
        if (/"(.*?)"/) {
            $op = "  " . join( ", ", map { s/[=:][sif]$//; /../ ? "--$_" : "-$_" } split /\|/, $1 );
            print $op;
        }
        print( " " x ( 30 - length($op) ) );

        s/.*#/#/;
        print;
    }

    say "
-l, -r, -t, and -b:
    By default, only the current branch is checked.  The 3 options (-l, -r,
    and -t) can be combined if needed.  If you don't want to search so many
    branches, use '-b' to specify which you want.  You can use it multiple times
    or supply a comma separated list.

--leafs_only:
    Normally, if you specify '-l', etc., all the corresponding refs are
    searched and matches reported  If you have too many local branches that
    all have the file, you'll get a lot of matches.  It becomes even more
    useless with tags, since there are often far more tags than branches.

    Specifying '-lo' would prevent that by searching only leaf refs (refs
    which are not part of some other ref).

    Be warned that this could miss some matches, for example if a file was
    deleted between master and pu, and you search '-l -lo', since pu is a
    leaf, it will be picked over master, and you won't find the file.  For
    such cases use '-b master' explicitly or don't use '-lo'.
";

    exit 1;
}

# ----------------------------------------------------------------------

# bare-minimum subset of 'Tsh' (see github.com/sitaramc/tsh)
{
    my ( $rc, $text );
    sub rc   { return $rc   || 0; }
    sub text { return $text || ''; }
    sub lines { return split /\n/, $text; }

    sub try {
        my $cmd = shift; die "try: expects only one argument" if @_;
        $text = `( $cmd ) 2>&1; echo -n RC=\$?`;
        if ( $text =~ s/RC=(\d+)$// ) {
            $rc = $1;
            return ( not $rc );
        }
        die "couldnt find RC= in result; this should not happen:\n$text\n\n...\n";
    }
}

sub dbg {
    use Data::Dumper;
    for my $i (@_) {
        print STDERR "DBG: " . Dumper($i);
    }
}

